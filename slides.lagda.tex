\documentclass{beamer}

% Use the input encoding UTF-8 and the font encoding T1.
\usepackage[utf8]{inputenc}
\usepackage[T1]{fontenc}

% Support for Agda code.
\usepackage{agda}

% Decrease the indentation of code.
\setlength{\mathindent}{1em}

% Customised setup for certain characters.
\usepackage{newunicodechar}
\newunicodechar{∋}{$\ni$}
% \newunicodechar{·}{$\cdot$}
\newunicodechar{⊢}{$\vdash$}
\newunicodechar{⋆}{${}^\star$}
\newunicodechar{Π}{$\Pi$}
\newunicodechar{⇒}{$\Rightarrow$}
\newunicodechar{ƛ}{$\lambdabar$}
\newunicodechar{∅}{$\emptyset$}
\newunicodechar{∀}{$\forall$}
\newunicodechar{ϕ}{$\Phi$}
\newunicodechar{ψ}{$\Psi$}
\newunicodechar{ρ}{$\rho$}
\newunicodechar{α}{$\alpha$}
\newunicodechar{β}{$\beta$}
\newunicodechar{μ}{$\mu$}
\newunicodechar{σ}{$\sigma$}
\newunicodechar{≡}{$\equiv$}
\newunicodechar{Γ}{$\Gamma$}
\newunicodechar{∥}{$\parallel$}
\newunicodechar{Λ}{$\Lambda$}
\newunicodechar{₀}{$~_0$}
\newunicodechar{₁}{$~_1$}
\newunicodechar{₂}{$~_2$}
\newunicodechar{θ}{$\theta$}
\newunicodechar{Θ}{$\Theta$}
\newunicodechar{∘}{$\circ$}
\newunicodechar{Δ}{$\Delta$}
\newunicodechar{λ}{$\lambda$}
\newunicodechar{⊧}{$\models$}
\newunicodechar{⊎}{$\uplus$}
\newunicodechar{η}{$\eta$}
\newunicodechar{⊥}{$\bot$}
\newunicodechar{Σ}{$\Sigma$}
\newunicodechar{ξ}{$\xi$}
\newunicodechar{ℕ}{$\mathbb{N}$}
\newunicodechar{ᶜ}{${}^c$}
\newunicodechar{Φ}{$\Phi$}
\newunicodechar{Ψ}{$\Psi$}
\newunicodechar{⊤}{$\top$}
\newunicodechar{≐}{$\doteq$}
\newunicodechar{≣}{$\triangleq$}
\newunicodechar{≃}{$\simeq$}
\newunicodechar{≅}{$\cong$}
\newunicodechar{∙}{$\bullet$}
\newunicodechar{ℓ}{$\ell$}
% \newunicodechar{}{$$}


\newunicodechar{∣}{\ensuremath{\mathnormal{\|}}}
\newunicodechar{∷}{\ensuremath{::}}
\newunicodechar{ℕ}{\ensuremath{\mathnormal{\mathbb{N}}}}
\newunicodechar{ℚ}{\ensuremath{\mathnormal{\mathbb{Q}}}}
\newunicodechar{ℤ}{\ensuremath{\mathnormal{\mathbb{Z}}}}
\newunicodechar{∥}{\ensuremath{\mathnormal{\||}}}

% Support for Greek letters.
\usepackage{alphabeta}

% Disable ligatures that start with '-'. Note that this affects the
% entire document!
\usepackage{microtype}
\DisableLigatures[-]{encoding=T1}

%Information to be included in the title page:
\title{Cubical Agda features}
\author{Guilherme Silva}
\date{11th September 2021}


\begin{document}

\frame{\titlepage}

\section{Cubical Agda}

\begin{frame}
  \frametitle{New Features}
  The most important features in Cubical Agda are:

  \begin{itemize}
    \item New Equality Type
    \item Quotient types
  \end{itemize}
\end{frame}

\section{Equalities}

\begin{frame}
  \frametitle{Equalities}
  There are multiple definitions of equalities and the most important are:
  \begin{itemize}
    \item Martin-Löf
    \item Leibniz
    \item Cubical
  \end{itemize}
\end{frame}

\begin{frame}
  \frametitle{Imports}
  \begin{code}
  {-# OPTIONS --cubical #-}

  module slides where

  open import Agda.Primitive.Cubical

  open import Cubical.Data.Unit
  open import Cubical.Data.Bool
  open import Cubical.Data.Int
  open import Cubical.Data.Nat hiding (_·_)
  open import Cubical.Data.Prod
  open import Cubical.Foundations.Isomorphism
  \end{code}

\end{frame}

\begin{frame}
  \frametitle{Martin-Löf equality}
  At the begin of Agda and in most theorems proves, equality is given by Martin-Löf's definition:
  \begin{code}
  module Martin-Löf {ℓ} {A : Set ℓ} where

    data _≡_ (x : A) : A → Set ℓ where
      refl : x ≡ x
  \end{code}
\end{frame}

\begin{frame}
  \frametitle{Martin-Löf pattern match}
  This equality is very convenient in proof assistances like Agda because it is possible to pattern match using them:

  \begin{code}
    private variable
      x y z : A

    sym  : x ≡ y → y ≡ x
    sym refl = refl

    trans : x ≡ y → y ≡ z → x ≡ z
    trans refl refl = refl
  \end{code}
\end{frame}

\begin{frame}
  \frametitle{Martin-Löf problem}

  But the problem of this equality is that it does not handle extensionality and other axioms very well:

  \begin{code}
  module FunExt {ℓ ℓ'} {A : Set ℓ} {B : Set ℓ'} where
    open Martin-Löf

    funExt-Type = {f g : A → B}
      → ((x : A) → f x ≡ g x) → f ≡ g
  \end{code}
\end{frame}

\begin{frame}
  \frametitle{Cubical Equality}
  To solve this problem, Agda adopted cubical type theory that equality is a function from the path to type:

  \begin{code}
  module CubicalEquality {ℓ} {A : Set ℓ} where
    postulate
      PathP : (A : I → Set ℓ) → A i0 → A i1 → Set ℓ

    _≡_ : A → A → Set ℓ
    _≡_ = PathP λ _ → A
  \end{code}
\end{frame}

\begin{frame}
  \frametitle{Cubical Equality Operators}
  From this equality, I will define reflection, symmetry and extensionality:

  \begin{code}
  module CubicalResults {ℓ ℓ'} {A : Set ℓ} {B : Set ℓ'} where
    open import Cubical.Core.Primitives

    private variable
      x y z : A

    refl : x ≡ x
    refl {x = x} = λ _ → x

    sym : x ≡ y → y ≡ x
    sym p i = p (~ i)

    funExt : {f g : A → B}
      → ((x : A) → f x ≡ g x) → f ≡ g
    funExt p i x = p x i
  \end{code}

  The operator $\sim$ invert the interval. If the interval \AgdaBound{i} goes from
  \AgdaBound{i0} to \AgdaBound{i1}, the interval $\sim i$ goes from \AgdaBound{i1} to \AgdaBound{i0}.
\end{frame}

\begin{frame}
  \frametitle{Cubical equality computational behavior}
  Another advantage of cubical equality is that it has a computational behavior.
  If there is a term that has a type that two types are equal, in this term, there is information of how they are equal.
  So this term represents the isomorphism of these two types.
\end{frame}

\begin{frame}
  \frametitle{Bool example}

  In this example, I will show the equality of two Bools:

  \begin{code}
  module CubicalIsomorphism where
    open import Cubical.Foundations.Prelude

    IsoBool : Iso Bool Bool
    IsoBool = iso not not ¬¬b ¬¬b
      where
        ¬¬b : ∀ b → not (not b) ≡ b
        ¬¬b false = refl
        ¬¬b true = refl

    Bool≡Bool : Bool ≡ Bool
    Bool≡Bool = isoToPath IsoBool

    _ : (transport Bool≡Bool false ≡ true)
      × (transport Bool≡Bool true ≡ false)
    _ = refl , refl

  \end{code}
\end{frame}

\section{Quotient Types}

\begin{frame}
  \frametitle{Quotient Types' Motivation}
  In simple type theories with quotient types,
  it is possible to create two distinct elements and after
  make them equal.

  One of the best examples is making $ \frac{1}{2} $ and $ \frac{2}{4} $ be equal elements.
\end{frame}

\begin{frame}
  \frametitle{Imports}
  \begin{code}

  open import Cubical.Foundations.Prelude hiding (isProp; isSet)
  open import Cubical.Relation.Nullary using (¬_)

  private variable
    ℓ ℓ₁ : Level
    A : Set ℓ
    B : Set ℓ₁
    x y z : A
  \end{code}
\end{frame}

\begin{frame}
  \frametitle{The simplest example of quotient types}
  This is an example when two elements of the same data types are equal:
  \begin{code}
  data Bool≡ : Set where
    true false : Bool≡
    t≡f : true ≡ false

  _ : true ≡ false
  _ = t≡f
  \end{code}
\end{frame}

\begin{frame}
  \frametitle{Functions of quotient types}
  Let a function f between elements of Bool≡ and another set, \\
  if $ x \equiv y $, then $ f(x) \equiv f(y) $.
  In this case, $ x $ is true, $ y $ is false and $ f(x) $ is tt.
  \begin{code}
  f : Bool≡ → Unit
  f true    = tt
  f false   = tt
  f (t≡f i) = refl i -- proving that f true ≡ f false

  \end{code}

\end{frame}

\begin{frame}
  \frametitle{Truncation type}

  In truncation type, every element of it is equal.
  Therefore we can define Bool≡ as ∥ Bool ∥:

  \begin{code}
  data ∥_∥ {ℓ} (A : Set ℓ) : Set ℓ where
    ∣_∣ : A → ∥ A ∥
    squash : ∀ (x y : ∥ A ∥) → x ≡ y

  Bool≡' = ∥ Bool ∥
  \end{code}
\end{frame}

\begin{frame}
  \frametitle{Not all equalities are the same}
  In cubical type theory, the equalities are not always the same.
  In this example, the circle is isomorphic (the same) to the integers:
  \begin{code}
  data Circle : Type where
    base : Circle
    loop : base ≡ base
  \end{code}
\end{frame}

\begin{frame}
  \frametitle{Making equalities equal}
  To make the equality equal, it is necessary when declaring a data type to state that it is a set:
  \begin{code}
  isProp : Type ℓ → Type ℓ
  isProp A = (x y : A) → x ≡ y

  isSet : Type ℓ → Type ℓ
  isSet A = (x y : A) → isProp (x ≡ y)
  \end{code}
\end{frame}

\begin{frame}
  \frametitle{Rational Numbers}
  Rational numbers are defined by numerator and a denominator different from zero.
  Two rational numbers $ \frac{p_1}{q_1} $ and $ \frac{p_2}{q_2} $ are equal when $ p_1 \times q_2 \equiv p_2 \times q_1 $:
  \begin{code}
  data ℚ : Type where
    _/_[_] : (p : ℤ) (q : ℤ) → ¬ (q ≡ pos 0) → ℚ
    path : ∀ p₁ q₁ p₂ q₂ {pr₁ pr₂} → (p₁ · q₂) ≡ (p₂ · q₁)
      → p₁ / q₁ [ pr₁ ] ≡ p₂ / q₂ [ pr₂ ]
    trunc : isSet ℚ

  _ : ∀ {pr₁ : ¬ (2 ≡ pos 0)} {pr₂ : ¬ (4 ≡ pos 0)}
    → 1 / 2 [ pr₁ ] ≡ 2 / 4 [ pr₂ ]
  _ = path 1 2 2 4 refl
  \end{code}
\end{frame}

\begin{frame}
  \frametitle{Integers}
  Another way to define integers is making the positive and negative zero equals:
  \begin{code}
  data ℤ' : Type where
    pos' : ℕ → ℤ'
    neg' : ℕ → ℤ'
    0+-≡ : pos' 0 ≡ neg' 0
  \end{code}
\end{frame}

\begin{frame}
  \frametitle{Sets}
  One way of defining sets is to create a list where you can swap elements and remove them when they are equal:
  \begin{code}
  infixr 20 _∷_
  data LFSet (A : Type ℓ) : Type ℓ where
    []    : LFSet A
    _∷_   : (x : A) → (xs : LFSet A) → LFSet A
    dup   : ∀ x xs   → x ∷ x ∷ xs ≡ x ∷ xs
    comm  : ∀ x y xs → x ∷ y ∷ xs ≡ y ∷ x ∷ xs
    trunc : isSet (LFSet A)

  _ : tt ∷ tt ∷ [] ≡ tt ∷ []
  _ = dup tt []

  _ : let true∷false : LFSet Bool
          true∷false = true ∷ false ∷ []
      in true∷false ≡ false ∷ true ∷ []
  _ = comm true false []
  \end{code}
\end{frame}

\end{document}
